LET EventData := input();
LET SqrlClock := TweetDate;

LET TweetText := jsonValue(EventData, '$.data.text');
LET AuthorId := jsonValue(EventData, '$.data.author_id');
LET TweetId := jsonValue(EventData, '$.data.id');
LET TweetDate := jsonValue(EventData, '$.data.created_at');
LET Users := jsonValue(EventData, '$.includes.users');
LET AuthorUserData := first([It for It in Users where jsonValue(It, "$.id") = AuthorId]);
LET AuthorUsername := entity("User", jsonValue(AuthorUserData, "$.username"));
LET AuthorCreatedAt := jsonValue(AuthorUserData, "$.created_at");
LET AuthorName := jsonValue(AuthorUserData, "$.name");
LET AuthorProfileImageUrl := jsonValue(AuthorUserData, "$.profile_image_url");

LET TweetTextSimhash := simhash(TweetText);

LET TweetsByUser := count(BY AuthorUsername TOTAL);
LET CountBySimhash := count(BY TweetTextSimhash LAST 1 HOUR);
LET UsersBySimhash := countUnique(AuthorUsername BY TweetTextSimhash LAST HOUR);

LET Global := true;
LET TotalTweets := count(BY Global);

CREATE RULE SimilarTweetText WHERE CountBySimhash>1 WITH REASON
    "Seen multiple tweets with similar text";
CREATE RULE SameTweetMultipleUsers WHERE UsersBySimhash>1 WITH REASON
    "Seen multiple users tweeting the same text";

WHEN SimilarTweetText THEN blockTweet();

log("Common tweet text (seen %d similar)" , CountBySimhash) WHERE CountBySimhash>1;
log("Seen %d tweets by user" , TweetsByUser) WHERE TweetsByUser>1;
