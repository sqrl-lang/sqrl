# Basic event set up
LET EventData := input();
LET SqrlClock := TweetDate;
LET DatasetId := 1;

# Feature extraction from the Twitter data
LET TweetText := jsonValue(EventData, '$.data.text');
LET AuthorId := jsonValue(EventData, '$.data.author_id');
LET TweetId := jsonValue(EventData, '$.data.id');
LET TweetDate := jsonValue(EventData, '$.data.created_at');
LET Users := jsonValue(EventData, '$.includes.users');
LET AuthorUserData := first([It for It in Users where jsonValue(It, "$.id") = AuthorId]);
LET AuthorUsername := entity("User", jsonValue(AuthorUserData, "$.username"));
LET AuthorCreatedAt := jsonValue(AuthorUserData, "$.created_at");
LET AuthorName := jsonValue(AuthorUserData, "$.name");
LET AuthorProfileImageUrl := jsonValue(AuthorUserData, "$.profile_image_url");

# These features follow a standard naming scheme, and allow rules to be shared between
# multiple websites, in our case the Wikipedia and Twitter demos.
LET UserGeneratedContent := TweetText;

# Simhash is a perceptual hash of text, such that similar text results in a similar hash
LET TweetTextSimhash := simhash(TweetText);

# Set up some counters
LET TweetsByUser := count(BY AuthorUsername TOTAL);
LET CountBySimhash := count(BY TweetTextSimhash LAST 5 MINUTES);
LET UsersBySimhash := countUnique(AuthorUsername BY TweetTextSimhash LAST 5 MINUTES);

# Simple rule to make sure atleast one tweet shows up in the UI
LET IsFirstTweetSeen := NOT rateLimited(BY DatasetId MAX 1 EVERY YEARS);
CREATE RULE FirstTweetSeen WHERE IsFirstTweetSeen;

# Simple rules for too much similar text
CREATE RULE SimilarTweetText WHERE CountBySimhash>2 WITH REASON
    "Seen ${CountBySimhash} tweets with similar text";
CREATE RULE SameTweetMultipleUsers WHERE UsersBySimhash>2 WITH REASON
    "Seen ${UsersBySimhash} users tweeting similar text";

# Flag any users using profanity (not a great spam rule! but easy)
LET BadWordMatches := patternMatches("bad-words.txt", UserGeneratedContent);
CREATE RULE UsedBadWords WHERE BadWordMatches WITH REASON
    "Matched simple profanity filter: ${BadWordMatches}";

# Look out for impersonated users
LET LooksLikeOlMusky := regexMatch("^E[1lI][0o]n.+Mu[$s]k.*$", AuthorName);
CREATE RULE ImpersonatedMusk WHERE LooksLikeOlMusky AND AuthorUsername != "elonmusk";

# This defines what gets shown in the sidebar
# At Twitter this might mean deleted tweets, suspending users, etc.
WHEN
    SimilarTweetText,
    SameTweetMultipleUsers,
    FirstTweetSeen,
    UsedBadWords,
    ImpersonatedMusk
THEN showEvent();

# Add some additional features if they are interesting
log("Seen %d tweets by user", TweetsByUser) WHERE TweetsByUser>1;